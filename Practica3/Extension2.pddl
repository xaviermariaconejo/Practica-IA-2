(define (domain viatge)
	(:requirements :strips :typing :adl :equality :fluents)
	(:types ciudad hotel vuelo)
	(:functions
		(actual)
		(ciudades-totales)
		(min-dias-totales)
		(min-dias-ciudad)
		(max-dias-ciudad)
		(dias-totales)
		(dias-ciudad ?c - ciudad)
		(interes ?c - ciudad)
		(interes-total)
	)
	(:predicates
		(es-de ?c - ciudad ?h - hotel)
		(va-a ?v - vuelo ?c1 - ciudad ?c2 - ciudad)
		(ciudad-empty ?c - ciudad)
		(esta-a ?c - ciudad)
		(inicial)
	)

	(:action ciudad-inicio
	  :parameters (?c - ciudad ?h - hotel)
	  :precondition (and (inicial) (es-de ?c ?h))
	  :effect (and 	(esta-a ?c)
	  			   	(increase (dias-totales) (min-dias-ciudad)) 
				   	(increase (dias-ciudad ?c) (min-dias-ciudad))
				   	(not (ciudad-empty ?c))
				   	(increase (actual) 1)
				   	(increase (interes-total) (interes ?c))
				   	(not (inicial))
		       )
	)

	(:action reservar-vol-hotel
	  	:parameters (?v - vuelo ?c1 - ciudad ?c2 - ciudad ?h - hotel)
		:precondition (and (not (inicial)) 
						   (esta-a ?c1) 
						   (not (ciudad-empty ?c1)) 
						   (ciudad-empty ?c2) 
						   (va-a ?v ?c1 ?c2)
						   (es-de ?c2 ?h)
					  )
		:effect (and (not (esta-a ?c1)) 
					 (esta-a ?c2) 
					 (not (ciudad-empty ?c2))
					 (increase (actual) 1)
					 (increase (dias-totales) (min-dias-ciudad)) 
					 (increase (dias-ciudad ?c2) (min-dias-ciudad))
					 (increase (interes-total) (interes ?c2))
				)
	)

	(:action mas-dias
	  :parameters (?c - ciudad)
	  :precondition (and (not (inicial)) (esta-a ?c) (not (>= (dias-ciudad ?c) (max-dias-ciudad))))
	  :effect (and (increase (dias-totales) 1) 
				   (increase (dias-ciudad ?c) 1))
	)
	
)