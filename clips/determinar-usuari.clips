; Mòdul per determinar tota l'informació i preferències de l'usuari

; @author Josep Sanchez Ferreres

; Input <- User
; Output -> Tipus viatge = {Cultural, Diversió, Negocis, Romàntic, Relax, Aventura}
;           Nivell vida = {Molt alt, alt, mig, baix}
;			Restriccions = {preu_min, preu_max}
;           Tipus client = {Grup amics, institut, Familia, Parella, Individual, Coworkers}
;           Preferències = {...}

;;
;;		Templates 
;;
(deftemplate num-persones "Nombre de persones en el viatge"
	(slot num (type INTEGER))
)

(deftemplate tipus-client "Arquetip de client"
	(slot tipus)
)

(deftemplate tipus-viatge "Arquetip de viatge"
	(slot tipus)
)
;;
;;		Inicialitzacions
;;
(deffacts init
	(num-persones (num -1))
	(tipus-client (tipus indefinit))
	(tipus-viatge (tipus indefinit))
)

;;
;;		Regles
;;
(defrule determinar-num-persones "Pregunta el nombre de persones a l'usuari"
	?c <-(num-persones (num -1))
=>
	(bind ?response (ask-integer "Per quantes persones és el viatge?"))
	(modify ?c (num ?response))
)

(defrule determinar-parella 
	(num-persones (num 2))
	?c<-(tipus-client (tipus indefinit))
	?v<-(tipus-viatge (tipus ?))

=>
	(if (si-o-no-p "És un viatge de parella?")
		then 
			(modify ?c (tipus parella) )
	else (if (si-o-no-p "Viatja per negocis?")
		then
			(modify ?v (tipus negocis))
			(modify ?c (tipus coworkers))
	))
)

;; @TODO Aquest 15 és completament arbitrari, quin numero hi posem? -setzer22
(defrule fi-de-curs "Si viatgen més de 15 persones probablement es tracti d'un viatge de fi de curs"
	(num-persones (num ?x&:(>= ?x 15))) 
	?c<-(tipus-client (tipus indefinit))
	?v<-(tipus-viatge (tipus ?))
=>
	(if (si-o-no-p "És un viatge per un institut?")
		then 
			(modify ?c (tipus institut) )
			;Els viatges d'institut solen ser de cultura o diversió
			(bind ?response (ask-question "L'enfoc del viatge és cultural o de diversio? Respon: {cultural, diversio, cap_dels_dos}" 
				cultural diversio cap_dels_dos))
			(if (neq ?response cap_dels_dos) then (modify ?v (tipus ?response)))
		
		else 
		(modify ?c (tipus grup_indefinit))
	)
)

