; Mòdul per determinar tota l'informació i preferències de l'usuari

; @author Josep Sanchez Ferreres

; Input <- User
; Output -> Tipus viatge = {Cultural, Diversio, Negocis, Romantic, Relax, Aventura}
;			Edat mitjana = {jove, adult, jubilats, variada}
;           Nivell vida = {molt_alt, alt, mig, baix}
;			Restriccions = {preu_min, preu_max}
;           Tipus client = {grup_organitzat, grup_amics, institut, familia, parella, individual, coworkers}
;           Preferències = {...}

;;
;;		Templates 
;;

(defmodule tipus_usuari "Tipus d'usuari, nivell de vida i preferències" 
	(export deftemplate ?ALL)
	(import utils deffunction ?ALL))

(deftemplate num-persones "Nombre de persones en el viatge"
	(slot num (type INTEGER))
)

(deftemplate tipus_client "Arquetip de client"
	(slot tipus)
)

(deftemplate tipus_viatge "Arquetip de viatge"
	(slot tipus)
)

(deftemplate edat_client "Categories d'edat"
	(slot edat)
)

;;
;;		Inicialitzacions
;;
(deffacts init
	(num-persones (num -1))
	(edat_client (edat indefinit))
	(tipus_client (tipus indefinit))
	(tipus_viatge (tipus indefinit))
)

;;
;;		Regles per determinar el tipus de client
;;
(defrule determinar-num-persones "Pregunta el nombre de persones a l'usuari"
	?c <-(num-persones (num -1))
=>
	(bind ?response (ask-integer "Per quantes persones és el viatge?"))
	(modify ?c (num ?response))
)

(defrule individual "Una persona sola és un individual"
	(num-persones (num 1))
	?c<-(tipus_client (tipus indefinit))
	?e <-(edat_client (edat indefinit))
=>
	(modify ?c (tipus individual))
	(modify ?e (edat adult)); Si viatja sol és adult. Podria ser un jubilat però en tot cas si
						  ; viatja sol tindrà unes preferències semblants a les d'un adult
)

(defrule determinar-parella "Decideix si el viatge és de parella. Altrament podria ser de negocis amb 2 persones"
	(num-persones (num 2))
	?c<-(tipus_client (tipus indefinit))
	?v<-(tipus_viatge (tipus ?))
	?e <-(edat_client (edat ?))
=>
	(if (si-o-no-p "És un viatge de parella?")
		then 
			(modify ?c (tipus parella) )
			; Les parelles joves solen tenir unes preferències semblants a les dels adults quan viatgen
			(modify ?e (edat adult) )
	else (if (si-o-no-p "Viatja per negocis?")
		then
			(modify ?v (tipus Negoci))
			(modify ?c (tipus coworkers))
			(modify ?e (edat adult) )
	))
)

(defrule institut "Si viatgen més de 15 persones probablement es tracti d'un viatge d'institut"
	(num-persones (num ?x&:(>= ?x 15))) 
	?c<-(tipus_client (tipus indefinit))
	?e <-(edat_client (edat ?))
=>
	(if (si-o-no-p "És un viatge per un institut?")
		then 
			(modify ?c (tipus institut) )
			(modify ?e (edat jove) )
		
		else 
		(modify ?c (tipus grup_indefinit))
	)
)


(defrule grups-grans "Pels grups grans mirem d'esbrinar quin és el tipus de grup amb el que estem tractant"
	(num-persones (num ?x&:(>= ?x 7))) 
	?c<-(tipus_client (tipus indefinit|grup_indefinit))
	?e <-(edat_client (edat ?))
=>
	(if (si-o-no-p "Sou un grup d'amics?") 
		then 
			(if (si-o-no-p "Menors de 25 anys?") then 
				(modify ?c (tipus grup_amics))
				(modify ?e (edat jove))
			 else (if (si-o-no-p "Jubilats?") then 
			 	(modify ?c (tipus grup_amics))
			 	(modify ?e (edat jubilats)
			 )))
		else 
			(if (si-o-no-p "Formeu part d'una organitzacio: equip de futbol, colla castellera...?") then (modify ?c (tipus grup_organitzat)))
	)
)

(defrule edat-grup-organitzat "Pels grups organitzats convé saber si hi entren nens"
	(tipus_client (tipus grup_organitzat))
	?e<-(edat_client (edat indefinit))
=>
	(if (si-o-no-p "Hi ha nens al grup?")
		then (modify ?e (edat variada))
		else (modify ?e (edat adult))
	)

(defrule families "Determinar si el grup és una familia amb fills"
	(num-persones (num ?x&:(> ?x 2)))
	?c<-(tipus_client (tipus indefinit))
	?e <-(edat_client (edat ?))
=>
	(if (si-o-no-p "Viatja en familia?") 
		then 
			(modify ?c (tipus familia))
			(modify ?e (edat variada))
	)
)

(defrule negocis "Preguntarem si el viatge és d'empresa"
	(num-persones (num ?x&:(!= ?x -1)))
	?c<-(tipus_client (tipus indefinit))
	?e <-(edat_client (edat ?))
=>
	(if (si-o-no-p "És un viatge d'empresa?") 
		then 
			(modify ?c (tipus coworkers))
			(modify ?e (edat adults))
	else
		(modify ?c (tipus familia))
		(modify ?e (edat adults))
	)
)



;;
;;		Regles per determinar l'objectiu del viatge
;;
(defrule tipus_institut "Els viatges d'institut solen ser enfocats a la cultura o a la diversió"
	(tipus_client (tipus institut))
	?v<-(tipus_viatge (tipus indefinit))
=>
	(bind ?response (ask-question "L'objectiu del viatge és d'aprenentatge o de diversio?" 
		Cultural Diversio cap_dels_dos))
	(if (neq ?response cap_dels_dos) 
		then (modify ?v (tipus ?response))
	; Una altra preferencia menys comú son els viatges d'aventura
	else (if (si-o-no-p "Preferiu un viatge d'aventura?")
		then (modify ?v (tipus Aventura))
		else (assert (obj_no_def)))
	)
)

(defrule objectiu_individual "Una persona viatjant sola probablement busca relax o diversió"
	(tipus_client (tipus individual))
	?v<-(tipus_viatge (tipus indefinit))
=>
	(bind ?response (ask-question "Prefereix un viatge de relax o diversió." Relax Diversio))
	(modify ?v (tipus ?response))
)

(defrule objectiu_parella "Les parelles solen voler viatges romàntics"
	(tipus_client (tipus parella))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(if (si-o-no-p "Li agradaria un viatge romàntic?")
		then
		(modify ?v (tipus Romantic))
		else 
		(modify ?v (tipus Relax))
	)
)

(defrule objectiu_empresa "Les empreses fan viatges d'empresa"
	(tipus_client (tipus coworkers))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(modify ?v (tipus Negoci))
)

(defrule grup_amics "Els grups joves busquen aventura o diversio"
	(tipus_client (tipus grup_amics))
	(edat_client (edat jove))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(if (si-o-no-p "Us agradaria visitar parcs tematics, espectacles o similars?") then
		(modify ?v (tipus Diversio))
	 else (modify ?v (tipus Aventura)))
)


(defrule objectiu_amics "Els grups de jubilats busquen relax"
	(tipus_client (tipus grup_amics))
	(edat_client (edat jubilats))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(modify ?v (tipus Relax))
)

(defrule grups_organitzats "Un grup organitzat sol tenir una temàtica molt concreta en ment"
	(tipus_client (tipus grup_organitzat))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(bind ?response (ask-question "Quin d'aquests tres tipus de viatge preferiu?" Aventura Diversio Relax))
	(modify ?v (tipus ?response))
)

(defrule familia "Les famílies tenen objectius de viatge diferent en funció de si es vol tenir contents als fills o als pares"
	(tipus_client (tipus familia))
	?v <- (tipus_viatge (tipus indefinit))
=>
	(if (si-o-no-p "Prefereixes un viatge per tenir als teus fills o per gaudir d'unes vacances") 
	then (modify ?v (tipus Diversio))
	else (modify ?v (ripus Relax)))
)

;
;		Extra
;
(defrule client-identificat "Un cop s'hagi identificat el client se salta al modul de preferències amb aquesta regla"
	(not (tipus_client (tipus indefinit)))
	(not (tipus_viatge (tipus indefinit)))
	(not (edat_client (edat indefinit)))
=>
	(focus preferencies)
)



;;
;;		[MODUL] Restriccins i preferèncie de l'usuari
;;

(defmodule preferencies "Estableix les preferencies de l'usuari"
	(export deftemplate ?ALL)
	(import tipus_usuari deftemplate ?ALL)
	(import utils deffunction ?ALL))

(deftemplate pressupost "Pressupost del viatge"
	(slot preu_min (type INTEGER))
	(slot preu_max (type INTEGER))
)

(deftemplate num_dies "Pressupost del viatge"
	(slot dies_min (type INTEGER))
	(slot dies_max (type INTEGER))
)

(deftemplate num_ciutats "Numero de ciutats a visitar"
	(slot min (type INTEGER))
	(slot max (type INTEGER))
)

(deftemplate nivell_vida "Nivell de vida de l'usuari"
	(slot valor))

(deftemplate llocs_exotics "Preferencies de llocs exotics"
	(slot valor))

(deffacts init-prefs
	(pressupost 
		(preu_min -1)
		(preu_max -1))

	(num_dies
		(dies_min -1)
		(dies_max -1))

	(num_ciutats
		(min -1)
		(max -1))

	(llocs_exotics (valor indefinit))

	(nivell_vida (valor indefinit))

)

(defrule init-preu
=>
(assert (importa_preu indefinit)))


(defrule minmax-dies
	?n <- (num_dies (dies_max -1) (dies_min -1))
=>
	; @TODO La pregunta és molt lletja... -setzer22
	(printout t "Quin nombre minim i maxim de dies vols viatjar?")
	(modify ?n (dies_min (ask-integer "minim: ")) (dies_max (ask-integer "maxim: "))) 
)

;@TODO afegir normes perquè si el pressupost no és coherent amb el perfil determinat faci preguntes addicionals per corregir
(defrule importa_el_preu "Determinem si a l'usuari li importa el preu"
	?n <- (nivell_vida (valor indefinit))
	?i <- (importa_preu indefinit)
=>
	(if (si-o-no-p "Li importa el preu del viatge?") then
		(retract ?i)
		(assert (importa_preu si))
		(if (si-o-no-p "Està disposat a sacrificar en qualitat per tal d'ajustar al màxim el pressupost?")
			then (modify ?n (valor baix))
			else (modify ?n (valor mig)))
	else
		(assert (importa_preu no))
		(retract ?i)
		(modify ?n (valor alt))
	)
)

(defrule minmax-preu
	?n <- (pressupost (preu_min -1) (preu_max -1))
	(importa_preu si)
=>
	(printout t "Indiqui pressupost minim i maxim pel viatge (preus per persona)")
	(modify ?n (preu_min (ask-integer "minim: ")) (preu_max (ask-integer "maxim: "))) 
)

(defrule minmax-ciutats
	?n <- (num_ciutats (min -1) (max -1))
=>
	(if (si-o-no-p "Prefereix centrar el seu viatge a una única ciutat") 
		then
			(modify ?n (min 1) (max 1))
		else
			(modify ?n (min (ask-integer "minim: ")) (max (ask-integer "maxim: "))) 
	)
)

(defrule exotics
	?n <- (llocs_exotics (valor indefinit))
=>
	; @TODO La pregunta és molt lletja... -setzer22
	(modify ?n (valor (si-o-no-p "Li agradaria viatjar per ciutats poc conegudes?")))
)

(defrule fi-modul
(declare (salience -10))
=>
(focus tipus_ciutat))
