;Input <- output(determinar-usuari)
;Output -> Tipus Ciutat(Molt Conueguda, Coneguda, Desconeguda)
;;         Cualitat Hotels(alta,mitja, baixa) 
;;         Tipus Activitats(Cultural, Aventura, Diversio, Relax)
;;

(defmodule tipus_ciutat "Tipus de ciutat, cualitat hotels i activitatss" 
	(export deftemplate ?ALL)
    (import tipus_usuari deftemplate ?ALL)
    (import preferencies deftemplate ?ALL)
    (import utils deffunction ?ALL)
)
;;
;;		Templates 
;;
(deftemplate tipus_ciutats "Arquetip de ciutats"
	(slot tipus)
)

(deftemplate cualitat_hotels "Arquetip de hotels"
	(slot tipus)
)

(deftemplate tipus_activitats "Arquetip d'activitat"
	(slot tipus) ; @TODO canviar aix√≤ per una llista -setzer22
)
;;
;;		Inicialitzacions
;;
(deffacts init
	(tipus_ciutats(tipus indefinit))
	(cualitat_hotels (tipus indefinit))
	(tipus_activitats (tipus indefinit))
)

;;
;;		Regles
;;

;;
;;TIPUS CIUTAT
;;


(defrule determinar-tipus-ciutat-institut "Tipus de viatge cultural institut o grup organitzat"
    (tipus_viatge(tipus Cultural))
    (or(tipus_client(tipus institut)) (tipus_client (tipus grup_organitzat)))
	?c <-(tipus_ciutats(tipus indefinit))
=>
	(modify ?c (tipus molt_coneguda))
)

(defrule determinar-tipus-ciutat-cultural "Tipus de viatge cultural"
    (tipus_viatge(tipus Cultural))
    (not(or(tipus_client(tipus institut)) (tipus_client (tipus grup_organitzat))))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus coneguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-diversio "Tipus de viatge diversio"
    (tipus_viatge(tipus Diversio))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus coneguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-negocis "Tipus de viatge negocis"
    (tipus_viatge(tipus Negoci))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus desconeguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-romantic "Tipus de viatge romantic"
    (tipus_viatge(tipus Romantic))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if(eq ?e TRUE) then 
			(modify ?c (tipus desconeguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-relax "Tipus de viatge relax"
    (tipus_viatge(tipus Relax))
    (or(tipus_client(tipus parella)) (tipus_client (tipus individual)))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus desconeguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-relax-dif "Tipus de viatge relax"
    (tipus_viatge(tipus Relax))
    (not(or(tipus_client(tipus parella)) (tipus_client (tipus individual))))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus coneguda))
			 else (modify ?c (tipus molt_coneguda)))
    
)

(defrule determinar-tipus-ciutat-aventura "Tipus de viatge aventura"
    (tipus_viatge(tipus Aventura))
	?c <-(tipus_ciutats(tipus indefinit))
    (llocs_exotics (valor ?e))
=>
    (if (eq ?e TRUE) then 
			(modify ?c (tipus desconeguda))
			 else (modify ?c (tipus coneguda)))
    
)




(defrule tipus-ciutat-desconeguda "Viatge a citutata desconeguda"
    (declare (salience -1))    
    ?c <- (tipus_ciutats(tipus indefinit))
    (llocs_exotics(valor ?p))  
=>
    (if (eq ?p TRUE) then 
    (modify ?c (tipus desconeguda))
    else (modify ?c (tipus molt_coneguda)))
)

 
;;
;;TIPUS D'ACTIVITATS
;;


(defrule tipus-activitats "Viatge amb activitats tipus"
    ?c <- (tipus_activitats(tipus indefinit))
    (not(tipus_viatge(tipus indefinit)))
    (tipus_viatge(tipus ?p))
=>
    (modify ?c (tipus ?p))
)

;;
;;CUALITAT HOTELS
;;

(defrule cualitat-hotel-ric "Hotel de persones que no importa el preu"
    (nivell_vida (valor alt))
    ?n <- (cualitat_hotels (tipus indefinit))
=>
    (modify ?n (tipus alta))
)

(defrule cualitat-hotel-mix "Hotel de persones que no importa una mica el preu"
    (nivell_vida (valor mig))
    ?n <- (cualitat_hotels (tipus indefinit))
    (pressupost(preu_min ?p))
    (num_dies(dies_min ?d))
=>
    (if (<(/ ?p ?d) 1500)
        then (modify ?n (tipus mitja))
        else (modify ?n (tipus alta)))
)

(defrule cualitat-hotel-baix "Hotel de persones que no importa una mica el preu"
    (nivell_vida (valor baix))
    ?n <- (cualitat_hotels (tipus indefinit))
    (pressupost(preu_min ?p))
    (num_dies(dies_min ?d))
=>
    (if (<(/ ?p ?d) 750)
        then (modify ?n (tipus baixa))
        else (modify ?n (tipus mitja)))
)


(defrule fi-modul-ciutat
(declare (salience -10))
=>
(focus fer-solucio))